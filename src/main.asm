.INCLUDE "macros.inc"
.INCLUDE "x16.inc"
.INCLUDE "serial.inc"
.SEGMENT "LOADADDR"
	.WORD $0801
.SEGMENT "BASICSTUB"
	.WORD START-2
	.BYTE $00,$00,$9E
	.BYTE "2061"
	.BYTE $00,$00,$00
.SEGMENT "STARTUP"
START:
	JMP MAIN
.SEGMENT "CODE"

MAIN:
	JSR INIT_IRQ
	JSR SETUP
	RTS
SETUP:
	LDA #INTR_SETUP				;ENABLE THE DATA READY INTERRUPT ON THE UART
	STA INTERRUPT_ENABLE		
	LDA #FIFO_SETUP				;ENABLE FIFO BUFFER
	STA FIFO_CONTROL			
	LDA #%10000000				;SETS BAUD RATE DIVISOR TO 1
	STA LINE_CONTROL			
	LDA #<BAUD_RATE_DIVISOR	
	STA DIVISOR_LATCH_LOW
	LDA #>BAUD_RATE_DIVISOR
	STA DIVISOR_LATCH_HI
	LDA #LCR_SETUP				;SETS WORD LENGTH TO 8 BITS
	STA LINE_CONTROL
	LDA #%00000011				;MAKES UART READY TO SEND AND RECIEVE DATA
	STA MODEM_CONTROL
	RTS
DEFAULT_IRQ: .ADDR 0
IRQ_HANDLING:
	INIT_IRQ:	
		LDA CINV				;SAVE THE DEFAULT IRQ POINTER
		STA DEFAULT_IRQ
		LDA CINV+1
		STA DEFAULT_IRQ+1
		SEI						;DISABLE INTERRUPTS
		LDA #<CUSTOM_IRQ		;REPLACE IRQ POINTER WITH CUSTOM POINTER
		STA CINV
		LDA #>CUSTOM_IRQ
		STA CINV+1
		CLI						;RE-ENABLE INTERRUPTS
		RTS
	CUSTOM_IRQ:
		JSR SERIAL_IRQ
		JMP (DEFAULT_IRQ)
	SERIAL_IRQ:
		LDA INTERRUPT_IDENT		;DETERMINE IF INTERRUPT IS CAUSED BY UART
		AND #%00000100
		CMP #%00000100
		BNE RETURN
		STZ MODEM_CONTROL		;STOP ALL TRANSFER OF DATA AFTER FIRST TRIGGER OF UART INTERRUPT
		JSR TRANSFER_DATA
	RETURN:
		RTS
TRANSFER_DATA:
		LDA RX_BUFFER				;READ DATA FROM THE UART AND STORE IT TO BANKED RAM
		STA VERA_DATA0				;PRINT SENT DATA TO SCREEN MEMORY
		LDA LINE_STATUS				;IF THERE IS STILL DATA IN THE FIFO
		AND #%00000001				;KEEP READING AND STORING THE DATA
		CMP #%00000001
		BEQ TRANSFER_DATA
		RTS
